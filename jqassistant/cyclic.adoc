[[cyclic:Default]]
[role=group,includesConstraints="cyclic:*(minor)",includesConcepts="cyclic:*"]
== Java

[[cyclic:ClassDependency]]
[source,cypher,role=concept"]
.Class Dependency
----
MATCH
    (c1:CLASS)-[:CONTAINS]->fom-[:DEPENDS_ON|ANNOTATED_BY|THROWS]->(c2:CLASS)
WHERE
    c1<>c2
CREATE UNIQUE
    (c1)-[:DEPENDS_ON]->(c2)
RETURN
    COUNT(c1) AS count
----


[[cyclic:PackageDependency]]
[source,cypher,role=concept,requiresConcept="cyclic:ClassDependency"]
.Package Dependency
----
MATCH
    (p1:PACKAGE)-[:CONTAINS]->(c1:CLASS)-[:DEPENDS_ON|ANNOTATED_BY|EXTENDS|IMPLEMENTS]->(c2:CLASS)<-[:CONTAINS]-(p2:PACKAGE)
WHERE
    c1<>c2 AND p1<>p2
CREATE UNIQUE
    (p1)-[:DEPENDS_ON]->(p2)
RETURN
    COUNT(p1) AS count
----

[[cyclic:CyclicPackageDependency]]
[source,cypher,role=constraint,requiresConcept="cyclic:PackageDependency"]
.There must be no cyclic package dependencies.
----
MATCH
    (p1:PACKAGE)-[:DEPENDS_ON]->(p2:PACKAGE),
        path2=shortestPath((p2)-[:DEPENDS_ON*]->(p1:PACKAGE))
WHERE
    p1<>p2
RETURN
    p1.FQN AS package, nodes(path2)
ORDER BY
    package
----

[[cyclic:CyclicClassDependency]]
[source,cypher,role=constraint,requiresConcept="cyclic:ClassDependency"]
.There must be no cyclic class dependencies.
----
MATCH
    (p1:PACKAGE)-[:DEPENDS_ON]->(p2:PACKAGE),
        shortestPath((p2)-[:DEPENDS_ON*]->(p1:PACKAGE))
WITH
    p1, p2
MATCH
    (p1)-[:CONTAINS]->(c1:CLASS),
    (c1)-[:DEPENDS_ON|ANNOTATED_BY|EXTENDS|IMPLEMENTS]->(c2),
        cpath2=shortestPath((c2)-[:DEPENDS_ON|ANNOTATED_BY|EXTENDS|IMPLEMENTS*]->(c1))
RETURN
    c1 AS class, nodes(cpath2)
----