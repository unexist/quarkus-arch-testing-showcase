[[complex:Default]]
[role=group,includesConstraints="complex:*(minor)",includesConcepts="complex:*"]
== Java

[[complex:ClassDependency]]
[source,cypher,role=concept"]
.Class Dependency
----
MATCH
    (c1:CLASS)-[:CONTAINS]->fom-[:DEPENDS_ON|ANNOTATED_BY|THROWS]->(c2:CLASS)
WHERE
    c1<>c2
CREATE UNIQUE
    (c1)-[:DEPENDS_ON]->(c2)
RETURN
    COUNT(c1) AS count
----

[[complex:PackageDependency]]
[source,cypher,role=concept,requiresConcept="complex:ClassDependency"]
.Package Dependency
----
MATCH
    (p1:PACKAGE)-[:CONTAINS]->(c1:CLASS)-[:DEPENDS_ON|ANNOTATED_BY|EXTENDS|IMPLEMENTS]->(c2:CLASS)<-[:CONTAINS]-(p2:PACKAGE)
WHERE
    c1<>c2 AND p1<>p2
CREATE UNIQUE
    (p1)-[:DEPENDS_ON]->(p2)
RETURN
    COUNT(p1) AS count
----

[[complex:CyclicPackageDependency]]
[source,cypher,role=constraint,requiresConcept="complex:PackageDependency"]
.There must be no complex package dependencies.
----
MATCH
    (p1:PACKAGE)-[:DEPENDS_ON]->(p2:PACKAGE),
        path2=shortestPath((p2)-[:DEPENDS_ON*]->(p1:PACKAGE))
WHERE
    p1<>p2
RETURN
    p1.FQN AS package, nodes(path2)
ORDER BY
    package
----

[[complex:CyclicClassDependency]]
[source,cypher,role=constraint,requiresConcept="complex:ClassDependency"]
.There must be no complex class dependencies.
----
MATCH
    (p1:PACKAGE)-[:DEPENDS_ON]->(p2:PACKAGE),
        shortestPath((p2)-[:DEPENDS_ON*]->(p1:PACKAGE))
WITH
    p1, p2
MATCH
    (p1)-[:CONTAINS]->(c1:CLASS),
    (c1)-[:DEPENDS_ON|ANNOTATED_BY|EXTENDS|IMPLEMENTS]->(c2),
        cpath2=shortestPath((c2)-[:DEPENDS_ON|ANNOTATED_BY|EXTENDS|IMPLEMENTS*]->(c1))
RETURN
    c1 AS class, nodes(cpath2)
----